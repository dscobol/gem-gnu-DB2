      **********************************************************
      * Program name:    UPDTROLL
      * Original author: David Stagowski
      *
      *    Description: A COBOL program that maintains a DB2 table
      *    using the UPDATE, DELETE, and INSERT SQL statements.
      *
      *    This version add COMMIT or ROLLBACK.
      *
      * Maintenance Log
      * Date       Author        Maintenance Requirement
      * ---------- ------------  --------------------------------
      * 2023-03-11 dscobol       Created to learn.
      * 2023-03-14 dscobol       Use env vars from .env file.
      * 20XX-XX-XX               If you change me, change this.
      *
      **********************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. UPDTROLL.

       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
      * SOURCE-COMPUTER.   IBM WITH DEBUGGING MODE.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT CUSTFile
           ASSIGN TO VFILE1
           ORGANIZATION IS INDEXED
           RECORD KEY IS CUSTFile-Cust-Number
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-CUSTFile-Status.

           SELECT ERRFile
           ASSIGN TO VFILE2
           ORGANIZATION IS INDEXED
           RECORD KEY IS ERRFile-Cust-Number
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-ERRFile-Status.

       DATA DIVISION.
       FILE SECTION.

       FD  CUSTFile.
       01  FD-CUSTFile-Customer-Record.
           COPY CUSTOMRS REPLACING ==:tag:== BY ==CUSTFile==.

       FD  ERRFile.
       01  FD-ERRFile-Customer-Record.
           COPY CUSTOMRS REPLACING ==:tag:== BY ==ERRFile==.

       WORKING-STORAGE SECTION.
       01  WS-FILE-STATUS.
           COPY WSFST REPLACING ==:tag:== BY ==CUSTFile==.
           COPY WSFST REPLACING ==:tag:== BY ==ERRFile==.

       01  WS-File-Counters.
           12 FD-CUSTFile-Record-Cnt         PIC S9(4) COMP VALUE ZERO.
           12 FD-ERRFile-Record-Cnt          PIC S9(4) COMP VALUE ZERO.
           12 DB-Insert-Record-Cnt           PIC S9(4) COMP VALUE ZERO.
           12 DB-Update-Record-Cnt           PIC S9(4) COMP VALUE ZERO.
           12 DB-Delete-Record-Cnt           PIC S9(4) COMP VALUE ZERO.
           12 WS-Display-Counters            PIC ZZZ9.

           EXEC SQL
               INCLUDE SQLCA
           END-EXEC.

           EXEC SQL
           DECLARE CUSTOMERS TABLE
           (
            CUSTNO        CHAR(6) not null,
           	FNAME         CHAR(20) not null,
           	LNAME         CHAR(30) not null,
           	ADDR          CHAR(30) not null,
           	CITY          CHAR(20) not null,
           	STATE         CHAR(2) not null,
           	ZIPCODE       CHAR(10) not null)
           END-EXEC.

           EXEC SQL BEGIN DECLARE SECTION END-EXEC.
       01  HV-Customer-Row.
           12 HV-Cust-Number          PIC X(06).
           12 HV-Cust-First-Name      PIC X(20).
           12 HV-Cust-Last-Name       PIC X(30).
           12 HV-Cust-Address         PIC X(30).
           12 HV-Cust-City            PIC X(20).
           12 HV-Cust-State           PIC X(02).
           12 HV-Cust-ZipCode         PIC X(10).

      *    DB1 connection parameters.
       01  DB1-DBName                 PIC X(8).
       01  DB1-User-ID                PIC X(10).
       01  DB1-Password.
           49 DB1-Passwd-Length       PIC S9(4) COMP-5  VALUE 0.
           49 DB1-Passwd-Name         PIC X(18).
           EXEC SQL END DECLARE SECTION END-EXEC.

       01 WS-SQL-STATUS                PIC S9(9) COMP-5.
          88 SQL-STATUS-OK             VALUE    0.
          88 SQL-STATUS-NOT-FOUND      VALUE  100.
          88 SQL-STATUS-DUP            VALUE -803.

       01 EOJ-Display-Messages.
           12 EOJ-End-Message PIC X(042) VALUE
              "*** Program UPDTROLL - End of Run Messages".

       PROCEDURE DIVISION.
       0000-Mainline.
           PERFORM 1000-Begin-Job.
           PERFORM 2000-Process.
           PERFORM 3000-End-Job.
           GOBACK.

       1000-Begin-Job.
           PERFORM 9800-Connect-to-DB1.
           IF SQL-STATUS-OK
              OPEN INPUT  CUSTFile
              OPEN OUTPUT ERRFile
              PERFORM 5000-Read-CUSTFile
           END-IF.

       2000-Process.
           PERFORM 2100-Update-Customer
              UNTIL WS-CUSTFile-EOF.

       2100-Update-Customer.
      D    DISPLAY "CustFile" FD-Custfile-Customer-Record.
           IF WS-CUSTFile-Good
              EVALUATE CUSTFile-Cust-Trans-Code
                 WHEN 'A'
                    PERFORM 2110-Insert-Customer-Row
                 WHEN 'U'
                    PERFORM 2120-Update-Customer-Row
                 WHEN 'D'
                    PERFORM 2130-Delete-Customer-Row
                 WHEN OTHER
                    PERFORM 2140-Write-Error-Record
              END-EVALUATE
           END-IF.
           PERFORM 5000-Read-CUSTFile.

       2110-Insert-Customer-Row.
           MOVE CUSTFile-Customer-Record TO
              HV-Customer-Row.
           PERFORM 6000-Insert-DB1.

       2120-Update-Customer-Row.
           MOVE CUSTFile-Customer-Record TO
              HV-Customer-Row.
           PERFORM 6100-Update-DB1.

       2130-Delete-Customer-Row.
           MOVE CUSTFile-Customer-Record TO
              HV-Customer-Row.
           PERFORM 6200-Delete-DB1.

       2140-Write-Error-Record.
           MOVE FD-CUSTFile-Customer-Record TO
                FD-ERRFile-Customer-Record.
           PERFORM 6000-Write-ERRFile.

       3000-End-Job.
           EXEC SQL CONNECT RESET END-EXEC.
           MOVE SQLCODE TO WS-SQL-STATUS.
           CLOSE CUSTFile
                 ERRFILE.
           DISPLAY EOJ-End-Message.
           MOVE FD-CUSTFile-Record-Cnt TO WS-Display-Counters.
           DISPLAY "      Records Read: " WS-Display-Counters.
           MOVE FD-ERRFile-Record-Cnt TO WS-Display-Counters.
           DISPLAY "Error Recs Written: " WS-Display-Counters.
           MOVE DB-Insert-Record-Cnt TO WS-Display-Counters.
           DISPLAY "  Records Inserted: " WS-Display-Counters.
           MOVE DB-Update-Record-Cnt TO WS-Display-Counters.
           DISPLAY "   Records Updated: " WS-Display-Counters.
           MOVE DB-Delete-Record-Cnt TO WS-Display-Counters.
           DISPLAY "   Records Deleted: " WS-Display-Counters.

       5000-Read-CUSTFile.
           READ CUSTFile
              AT END SET WS-CUSTFile-EOF TO TRUE
           END-READ.
           EVALUATE TRUE
              WHEN WS-CUSTFile-Good
                 ADD +1 TO FD-CUSTFile-Record-Cnt
              WHEN WS-CUSTFile-EOF
                 CONTINUE
              WHEN OTHER
                 DISPLAY "** ERROR **: 5000-Read-CUSTFile"
                 DISPLAY "Read CUSTFile Failed."
                 DISPLAY "File Status: " WS-CUSTFile-Status
                 PERFORM 3000-End-Job
                 MOVE 8 TO RETURN-CODE
                 GOBACK
           END-EVALUATE.

       6000-Write-ERRFile.
           WRITE FD-ERRFile-Customer-Record.
           EVALUATE TRUE
              WHEN WS-ERRFile-Good
                 ADD +1 TO FD-ERRFile-Record-Cnt
              WHEN OTHER
                 DISPLAY "** ERROR **: 6000-Write-ERRFile"
                 DISPLAY "Write ERRFile Failed."
                 DISPLAY "File Status: " WS-ERRFile-Status
                 PERFORM 3000-End-Job
                 MOVE 8 TO RETURN-CODE
                 GOBACK
           END-EVALUATE.

       6000-Insert-DB1.
           EXEC SQL
              INSERT INTO CUSTOMERS
               (CUSTNO, FNAME, LNAME,
                ADDR, CITY, STATE,
                ZIPCODE)
              VALUES
                (:HV-Cust-Number,
                 :HV-Cust-First-Name,
                 :HV-Cust-Last-Name,
                 :HV-Cust-Address,
                 :HV-Cust-City,
                 :HV-Cust-State,
                 :HV-Cust-ZipCode)
           END-EXEC.
           MOVE SQLCODE TO WS-SQL-STATUS.
           EVALUATE TRUE
              WHEN SQL-STATUS-OK
                 ADD +1 TO DB-Insert-Record-Cnt
                 EXEC SQL COMMIT END-EXEC
              WHEN OTHER
                 DISPLAY "*** WARNING ***"
                 DISPLAY "There was a problem Inserting the record."
                 DISPLAY "SQLCODE = " SQLCODE
                 EXEC SQL ROLLBACK END-EXEC
                 PERFORM 3000-End-Job
                 MOVE 8 TO RETURN-CODE
                 GOBACK
           END-EVALUATE.

       6100-Update-DB1.
           EXEC SQL
              UPDATE CUSTOMERS
              SET
               FNAME   = :HV-Cust-First-Name,
               LNAME   = :HV-Cust-Last-Name,
               ADDR    = :HV-Cust-Address,
               CITY    = :HV-Cust-City,
               STATE   = :HV-Cust-State,
               ZIPCODE = :HV-Cust-ZipCode
              WHERE
                CUSTNO = :HV-Cust-Number
           END-EXEC.
           MOVE SQLCODE TO WS-SQL-STATUS.
           EVALUATE TRUE
              WHEN SQL-STATUS-OK
                 ADD +1 TO DB-Update-Record-Cnt
                 EXEC SQL COMMIT END-EXEC
              WHEN OTHER
                 DISPLAY "*** WARNING ***"
                 DISPLAY "There was a problem Updating the record."
                 DISPLAY "SQLCODE = " SQLCODE
                 EXEC SQL ROLLBACK END-EXEC
                 PERFORM 3000-End-Job
                 MOVE 8 TO RETURN-CODE
                 GOBACK
           END-EVALUATE.

       6200-Delete-DB1.
           EXEC SQL
              DELETE FROM CUSTOMERS
              WHERE
                CUSTNO = :HV-Cust-Number
           END-EXEC.
           MOVE SQLCODE TO WS-SQL-STATUS.
           EVALUATE TRUE
              WHEN SQL-STATUS-OK
                 ADD +1 TO DB-Delete-Record-Cnt
                 EXEC SQL COMMIT END-EXEC
              WHEN OTHER
                 DISPLAY "*** WARNING ***"
                 DISPLAY "There was a problem Deleting the record."
                 DISPLAY "SQLCODE = " SQLCODE
                 EXEC SQL ROLLBACK END-EXEC
                 PERFORM 3000-End-Job
                 MOVE 8 TO RETURN-CODE
                 GOBACK
           END-EVALUATE.

       9800-Connect-to-DB1.
           PERFORM 9810-Setup-DB1-Connection.
           IF SQL-STATUS-OK
              CONTINUE
           ELSE
              DISPLAY "*** The DB connection is not valid!***"
              DISPLAY "Exiting the program.!"
              GOBACK
           END-IF.

       9810-Setup-DB1-Connection.
           PERFORM 9811-Get-Credentials.
           PERFORM 9814-Connect-To-DB1.

       9811-Get-Credentials.
           ACCEPT DB1-DBName FROM ENVIRONMENT 'db2name'.
           ACCEPT DB1-User-ID FROM ENVIRONMENT 'username'.
           ACCEPT DB1-Passwd-Name FROM ENVIRONMENT 'password'.

      *    Passwords in a CONNECT statement should be entered
      *    in a VARCHAR format with the length of the input string.
           INSPECT DB1-Passwd-Name
              TALLYING DB1-Passwd-Length
              FOR CHARACTERS BEFORE INITIAL " ".

      D    DISPLAY "DB1-DBName " DB1-DBName.
      D    DISPLAY "DB1-User-ID " DB1-User-ID.
      D    DISPLAY "DB1-Passwd-Name " DB1-Passwd-Name.

       9814-Connect-To-DB1.
           EXEC SQL CONNECT TO :DB1-DBName
                USER :DB1-User-ID
                USING :DB1-Password
              END-EXEC.
           MOVE SQLCODE TO WS-SQL-STATUS.
